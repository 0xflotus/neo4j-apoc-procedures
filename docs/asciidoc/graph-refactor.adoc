[[graph-refactoring]]
== Graph Refactoring

[cols="1m,5"]
|===
| call apoc.refactor.cloneNodes([node1,node2,...]) |  clone nodes with their labels and properties
| call apoc.refactor.cloneNodesWithRelationships([node1,node2,...]) | clone nodes with their labels, properties and relationships
| call apoc.refactor.cloneSubgraph([node1,node2,...], [rel1,rel2,...]=[], {standinNodes:[[oldNode1, standinNode1], ...], skipProperties:[prop1, prop2, ...]}={}) YIELD input, output, error |
clone nodes with their labels and properties (optionally skipping any properties in the skipProperties list via the config map), and clone the given relationships (will exist between cloned nodes only). If no relationships are provided, all relationships between the given nodes will be cloned. Relationships can be optionally redirected according to standinNodes node pairings (this is a list of list-pairs of nodes), so given a node in the original subgraph (first of the pair), an existing node (second of the pair) can act as a standin for it within the cloned subgraph. Cloned relationships will be redirected to the standin.
| call apoc.refactor.cloneSubgraphFromPaths([path1,path2,...], {standinNodes:[[oldNode1, standinNode1], ...], skipProperties:[prop1, prop2, ...]}={}) YIELD input, output, error |
from the subgraph formed from the given paths, clone nodes with their labels and properties (optionally skipping any properties in the skipProperties list via the config map),
and clone the relationships (will exist between cloned nodes only). Relationships can be redirected according to optional standinNodes node pairings (this is a list of list-pairs of nodes), so given a node in the original subgraph (first of the pair), an existing node (second of the pair) can act as a standin for it within the cloned subgraph. Cloned relationships will be redirected to the standin.
| call apoc.refactor.mergeNodes([node1,node2]) | merge nodes onto first in list
| call apoc.refactor.mergeRelationships([rel1,rel2,...],{config}) | merge relationships onto first in list
| call apoc.refactor.to(rel, endNode) | redirect relationship to use new end-node
| call apoc.refactor.from(rel, startNode) | redirect relationship to use new start-node
| call apoc.refactor.invert(rel) | inverts relationship direction
| call apoc.refactor.setType(rel, 'NEW-TYPE') | change relationship-type
| call apoc.refactor.extractNode([rel1,rel2,...], [labels], 'OUT','IN') | extract node from relationships
| call apoc.refactor.collapseNode([node1,node2],'TYPE') | collapse nodes with 2 rels to relationship, node with one rel becomes self-relationship
| call apoc.refactor.normalizeAsBoolean(entity, propertyKey, true_values, false_values) | normalize/convert a property to be boolean
| call apoc.refactor.categorize(node, propertyKey, type, outgoing, label) | turn each unique propertyKey into a category node and connect to it
|===

On mergeRelationship and mergeNodes with config properties you can choose from 3 different management:
 * "overwrite" or "override": if there is the same property in more relationship, in the new one will have the last relationship's property value
 * "discard" : if there is the same property in more relationship, the new one will have the first relationship's property value
 * "combine" : if there is the same property in more relationship, the new one a value's array with all relationships' values

On mergeNodes procedure there is also a config parameter to merge relationships of the merged nodes:
 * "mergeRels: true/false" : give the possibility to merge relationships with same type and direction.

If relationships have same start and end nodes will be merged into one, and properties managed by the properties config.
If relationships have different start/end nodes (related to direction), relationships will be maintained and properties will be combine in all relationship.

[[clone-nodes]]
=== Clone nodes

We create a dataset
[source,cypher]
----
CREATE (f:Foo{name:'Foo'}),(b:Bar{name:'Bar'})
----

As result we have two nodes

image::{img}/apoc.refactor.cloneNodes.dataset.png[width=800]

[source,cypher]
----
MATCH (f:Foo{name:'Foo'}),(b:Bar{name:'Bar'}) WITH f,b
CALL apoc.refactor.cloneNodes([f,b]) yield input, output RETURN *
----

As result we have the two nodes that we have created before and their clones

image::{img}/apoc.refactor.cloneNodes.png[width=800]

.Clone nodes with relationship

We create a dataset of two different nodes of type `Actor` connected with other two different node of type `Movie`

[source,cypher]
----
CREATE (k:Actor {name:'Keanu Reeves'})-[:ACTED_IN {role:'Neo'}]->(m:Movie {title:'The Matrix'}),
	   (t:Actor {name:'Tom Hanks'})-[:ACTED_IN {role:'Forrest'}]->(f:Movie {title:'Forrest Gump'}) RETURN *
----

image::{img}/apoc.refactor.cloneNodesWithRelationships.dataset.png[width=800]

[source,cypher]
----
MATCH (k:Actor {name:'Keanu Reeves'}), (t:Actor {name:'Tom Hanks'})
CALL apoc.refactor.cloneNodesWithRelationships([k,t]) YIELD input, output RETURN *
----

As result we have a copy of the nodes and relationships

image::{img}/apoc.refactor.cloneNodesWithRelationships.png[width=800]

[[clone-subgraph]]
=== Clone subgraph

You can use the `cloneSubgraph()` and `cloneSubraphFromPaths()` to clone a subgraph defined either by a list of nodes and a list of relationships, or a list of paths.
This is useful when you want to ensure the cloned subgraph isn't connected to the original nodes, or to nodes outside the subgraph.

If relationships are not provided, then all relationships between the given nodes will be cloned.

In the config map, we can supply a `standinNodes` list (of pairs of nodes), allowing an existing node in the graph to act as a standin for another node in the cloned subgraph.
This can be useful when you want to attach the cloned subgraph to another node in your graph (in place of cloning a node).

For example, when cloning a tree from one root to another.

Let's create a dataset of two trees:

[source,cypher]
----
CREATE  (rootA:Root{name:'A'}),
        (rootB:Root{name:'B'}),
        (n1:Node{name:'node1', id:1}),
        (n2:Node{name:'node2', id:2}),
        (n3:Node{name:'node3', id:3}),
        (n4:Node{name:'node4', id:4}),
        (n5:Node{name:'node5', id:5}),
        (n6:Node{name:'node6', id:6}),
        (n7:Node{name:'node7', id:7}),
        (n8:Node{name:'node8', id:8}),
        (n9:Node{name:'node9', id:9}),
        (n10:Node{name:'node10', id:10}),
        (n11:Node{name:'node11', id:11}),
        (n12:Node{name:'node12', id:12})
        CREATE (rootA)-[:LINK]->(n1)-[:LINK]->(n2)-[:LINK]->(n3)-[:LINK]->(n4)
        CREATE               (n1)-[:LINK]->(n5)-[:LINK]->(n6)<-[:LINK]-(n7)
        CREATE                             (n5)-[:LINK]->(n8)
        CREATE                             (n5)-[:LINK]->(n9)-[:DIFFERENT_LINK]->(n10)
        CREATE (rootB)-[:LINK]->(n11)
----

For our use case, we want to clone a subtree starting from rootA consisting only of outgoing :LINK relationships,
and attach that subgraph to rootB. rootB becomes the standin for rootA (which is not cloned).

[source,cypher]
----
MATCH  (rootA:Root{name:'A'}),
        (rootB:Root{name:'B'})
MATCH path = (rootA)-[:LINK*]->(node)
WITH rootA, rootB, collect(path) as paths
CALL apoc.refactor.cloneSubgraphFromPaths(paths, {standinNodes:[[rootA, rootB]]}) YIELD input, output, error
RETURN input, output, error
----

A subsequent MATCH to the entire graph will show the subgraph has been cloned and attached to rootB.

We can instead use `apoc.refactor.cloneSubgraph()`, providing the lists of nodes and relationships which form the subgraph.
We can get the nodes and rels from the yielded output of `apoc.path.subgraphAll()`, and we can filter to the relationship types we want in the call.

After clearing and repopulating the tree graph, we can run:

[source,cypher]
----
MATCH  (rootA:Root{name:'A'}),
        (rootB:Root{name:'B'})
CALL apoc.path.subgraphAll(rootA, {relationshipFilter:'LINK>'}) YIELD nodes, relationships
CALL apoc.refactor.cloneSubgraph(nodes, [rel in relationships WHERE type(rel) = 'LINK'], {standinNodes:[[rootA, rootB]]}) YIELD input, output, error
RETURN input, output, error
----

The resulting graph will be the same as our earlier `apoc.refactor.cloneSubgraphFromPaths()` call.


////
=== Merge nodes

We create two nodes with different properties

[source,cypher]
----
CREATE (f:Person {name:'Foo'}), (b:Person {surname:'Bar'}) RETURN f,b
----

image::{img}/apoc.refactor.mergeNodes.dataset.png[width=800]

Now we want to merge these nodes into one

[source,cypher]
----
MATCH (f:Person {name:'Foo'}), (b:Person {surname:'Bar'})
CALL apoc.refactor.mergeNodes([f,b])
YIELD node RETURN node
----

image::{img}/apoc.refactor.mergeNodes.png[width=800]

Thus we have one node with both properties `name` and `surname`
////

[[merge-nodes]]
=== Merge Nodes

You can merge a list of nodes onto the first one in the list.

All relationships are merged onto that node too.
You can specify the merge behavior for properties globally and/or individually.

[source,cypher]
---
MATCH (p:Person)
WITH p ORDER BY p.created DESC // newest one first
WITH p.email, collect(p) as nodes
CALL apoc.refactor.mergeNodes(nodes, {properties: {name:'discard', age:'overwrite', kids:'combine', `addr.*`, 'overwrite',`.*`: 'discard'}}) YIELD node
RETURN node
---

This config option also works for `apoc.refactor.mergeRelationships([rels],{config})`.

[opts=header]
|===
| type | operations
| discard | the first nodes' property will remain if already set, otherwise the first property in list will be written
| overwrite / override |Â last property in list wins
| combine | if there is only one property in list, it will be set / kept as single property otherwise create an array, tries to coerce values
|===

For mergeNodes you can Merge relationships with same type and direction, you can spacify this with property mergeRels.
Relationships properties are managed with the same nodes' method, if properties parameter isn't set relationships properties are combined.

.example1 - Relationships with same start and end nodes

First of all we have to create nodes and relationships

[source,cypher]
----
Create (n1:Person {name:'Tom'}),
(n2:Person {name:'John'}),
(n3:Company {name:'Company1'}),
(n5:Car {brand:'Ferrari'}),
(n6:Animal:Cat {name:'Derby'}),
(n7:City {name:'London'}),
(n1)-[:WORKS_FOR{since:2015}]->(n3),
(n2)-[:WORKS_FOR{since:2018}]->(n3),
(n3)-[:HAS_HQ{since:2004}]->(n7),
(n1)-[:DRIVE{since:2017}]->(n5),
(n2)-[:HAS{since:2013}]->(n6)
return *;
----

image::{img}/apoc.refactor.mergeNodes.createDataSetFirstExample.png[]

Next step is calling the apoc to merge nodes :Person

[source,cypher]
----
MATCH (a1:Person{name:'John'}), (a2:Person {name:'Tom'})
WITH head(collect([a1,a2])) as nodes
CALL apoc.refactor.mergeNodes(nodes,{properties:"combine", mergeRels:true}) yield node
MATCH (n)-[r:WORKS_FOR]->(c) return *
----

and the result is:

image::{img}/apoc.refactor.mergeNodes.resultFirstExample.png[]

In this case we have relationships with same start and end nodes so relationships are merged into one and properties are combined.

Relationships with different start or end nodes

[source,cypher]
----
Create (n1:Person {name:'Tom'}),
(n2:Person {name:'John'}),
(n3:Company {name:'Company1'}),
(n4:Company {name:'Company2'}),
(n5:Car {brand:'Ferrari'}),
(n6:Animal:Cat {name:'Derby'}),
(n7:City {name:'London'}),
(n8:City {name:'Liverpool'}),
(n1)-[:WORKS_FOR{since:2015}]->(n3),
(n2)-[:WORKS_FOR{since:2018}]->(n4),
(n3)-[:HAS_HQ{since:2004}]->(n7),
(n4)-[:HAS_HQ{since:2007}]->(n8),
(n1)-[:DRIVE{since:2017}]->(n5),
(n2)-[:HAS{since:2013}]->(n6)
return *;
----

image::{img}/apoc.refactor.mergeNodes.createDataSetSecondExample.png[]

Next step is calling the apoc to merge nodes :Person

[source,cypher]
----
MATCH (a1:Person{name:'John'}), (a2:Person {name:'Tom'})
WITH head(collect([a1,a2])) as nodes
CALL apoc.refactor.mergeNodes(nodes,{properties:"combine", mergeRels:true}) yield node
MATCH (n)-[r:WORKS_FOR]->(c) return n.name,r.since,c.name;
----

and the result is:

image::{img}/apoc.refactor.mergeNodes.resultSecondExample.png[]

image::{img}/apoc.refactor.mergeNodes.resultSecondExampleData.png[]

In this case we have relationships with different end nodes so all relationships are maintained and properties are combined into all relationships.

[source,cypher]
----
MATCH (p:Person)
WITH p ORDER BY p.created DESC // newest one first
WITH p.email, collect(p) as nodes
CALL apoc.refactor.mergeNodes(nodes, {properties:'combine', mergeRels: true}) YIELD node
RETURN node
----

[[redirect-relationship]]
=== Redirect relationships

==== Redirect Source Node

We start with two nodes related each other with a relationship. We create a new node which we will use to redirect the relationship like end node

[source,cypher]
----
CREATE (f:Foo)-[rel:FOOBAR {a:1}]->(b:Bar)
CREATE (p:Person {name:'Antony'})
RETURN *
----

image::{img}/apoc.refactor.to.dataset.png[width=800]

[source,cypher]
----
MATCH (f:Foo)-[rel:FOOBAR {a:1}]->(b:Bar) with id(rel) as id
MATCH (p:Person {name:'Antony'}) with p as p
MATCH ()-[r]->(), (p:Person)  CALL apoc.refactor.to(r, p) YIELD input, output RETURN *
----

image::{img}/apoc.refactor.to.png[width=800]

Now the relationship is towards the new node `Person`

==== Redirect Target Node

We start with two nodes related each other with a relationship. We create a new node which we will use to redirect the relationship like start node

[source,cypher]
----
CREATE (f:Foo)-[rel:FOOBAR {a:1}]->(b:Bar)
CREATE (p:Person {name:'Antony'})
RETURN *
----

image::{img}/apoc.refactor.from.dataset.png[width=800]

[source,cypher]
----
MATCH (f:Foo)-[rel:FOOBAR {a:1}]->(b:Bar) with id(rel) as id
MATCH (p:Person {name:'Antony'}) with p as p
MATCH ()-[r]->(), (p:Person)  CALL apoc.refactor.from(r, p) YIELD input, output RETURN *
----

image::{img}/apoc.refactor.from.png[width=800]

Now the relationship starts from the new node `Person` from the old node `Bar`

[[invert-relationship]]
=== Invert relationship

We start with two nodes connected by a relationship

[source,cypher]
----
CREATE (f:Foo)-[rel:FOOBAR {a:1}]->(b:Bar)
----

image::{img}/apoc.refactor.invert.dataset.png[width=800]

Now we want to invert the relationship direction

[source,cypher]
----
MATCH (f:Foo)-[rel:FOOBAR {a:1}]->(b:Bar) WITH id(rel) as id
MATCH ()-[r]->() WHERE id(r) = id
CALL apoc.refactor.invert(r) yield input, output RETURN *
----

image::{img}/apoc.refactor.invert.call.png[width=800]

image::{img}/apoc.refactor.invert.png[width=800]

[[set-relationship-type]]
=== Set Relationship Tsype

With a simple relationship between two node

[source,cypher]
----
CREATE (f:Foo)-[rel:FOOBAR]->(b:Bar)
----

image::{img}/apoc.refactor.setType.dataset.png[width=800]

We can change the relationship type from `FOOBAR` to `NEW-TYPE`

[source,cypher]
----
MATCH (f:Foo)-[rel:FOOBAR]->(b:Bar) with rel
CALL apoc.refactor.setType(rel, 'NEW-TYPE') YIELD input, output RETURN *
----

image::{img}/apoc.refactor.setType.png[width=800]

[[extract-node-from-relationship]]
=== Extract node from relationships

[source,cypher]
----
CREATE (f:Foo)-[rel:FOOBAR {a:1}]->(b:Bar)
----

image::{img}/apoc.refactor.extractNode.dataset.png[width=800]

We pass the ID of the relationship as parameter to extract a node

[source,cypher]
----
MATCH (f:Foo)-[rel:FOOBAR {a:1}]->(b:Bar) WITH id(rel) as id
CALL apoc.refactor.extractNode(id,['FooBar'],'FOO','BAR')
YIELD input, output RETURN *
----

image::{img}/apoc.refactor.extractNode.png[width=800]

[[collapse-node-to-relationship]]
=== Collapse node to relationship

[source,cypher]
----
CREATE (f:Foo)-[:FOO {a:1}]->(b:Bar {c:3})-[:BAR {b:2}]->(f) WITH id(b) as id
CALL apoc.refactor.collapseNode(id,'FOOBAR')
YIELD input, output RETURN *
----

Before we have this situation

image::{img}/apoc.refactor.collapseNode.dataset.png[width=800]

And the result are

image::{img}/apoc.refactor.collapseNode.png[width=800]

The property of the two relationship and the property of the node are joined in one relationship that has the  properties `a:1`, `b:2`, `name:Bar`

=== Normalize As Boolean

[source,cypher]
----
CREATE (:Person {prop: 'Y', name:'A'}),(:Person {prop: 'Yes', name:'B'}),(:Person {prop: 'NO', name:'C'}),(:Person {prop: 'X', name:'D'})
----

As a resul we have four nodes with different properties `prop` like `Y`, `Yes`, `NO`, `X`

image::{img}/apoc.refactor.normalizeAsBoolean.dataset.png[width=800]

Now we want to transform some properties into a boolean, `Y`, `Yes` into true and the properties `NO` into false.
The other properties that don't match these possibilities will be set as `null`.

[source,cypher]
----
MATCH (n)  CALL apoc.refactor.normalizeAsBoolean(n,'prop',['Y','Yes'],['NO']) WITH n ORDER BY n.id RETURN n.prop AS prop
----

image::{img}/apoc.refactor.normalizeAsBoolean.png[width=800]

[[categorize]]
=== Categorize

Categorize replaces string property values on nodes with relationship to a unique category node for that property value.

This example will turn all n.color properties into :HAS_ATTRIBUTE relationships to :Color nodes with a matching .colour property.

[source,cypher]
----
CALL apoc.refactor.categorize('color','HAS_ATTRIBUTE',true,'Color','colour',['popularity'],1)
----

Additionally, it will also copy over the first 'popularity' property value encountered on any node n for each newly created :Color node and remove any occurrences of that property value on nodes with the same 'Color'.

=== Using Cypher and APOC to move a property value to a label

You can use the procedure `apoc.create.addLabels` to move a property to a label with Cypher as follows

.Create a node with property studio
[source,cypher]
----
CREATE (:Movie {title: 'A Few Good Men', genre: 'Drama'})
----

.Move the 'genre' property to a label and remove it as a property
[source,cypher]
----
MATCH (n:Movie) 
CALL apoc.create.addLabels( id(n), [ n.genre ] ) YIELD node 
REMOVE node.genre
RETURN node
----


[[rename-label-type-property]]
=== Rename

Procedures set for renaming labels, relationship types, nodes and relationships' properties.
They return the list of eventually impacted constraints and indexes, the user should take care of.

[cols="1m,5"]
|===
| call apoc.refactor.rename.label(oldLabel, newLabel, [nodes]) | rename a label from 'oldLabel' to 'newLabel' for all nodes. If 'nodes' is provided renaming is applied to this set only
| call apoc.refactor.rename.type(oldType, newType, [rels]) | rename all relationships with type 'oldType' to 'newType'. If 'rels' is provided renaming is applied to this set only
| call apoc.refactor.rename.nodeProperty(oldName, newName, [nodes]) | rename all node's property from 'oldName' to 'newName'. If 'nodes' is provided renaming is applied to this set only
| call apoc.refactor.rename.typeProperty(oldName, newName, [rels]) | rename all relationship's property from 'oldName' to 'newName'. If 'rels' is provided renaming is applied to this set only
|===
