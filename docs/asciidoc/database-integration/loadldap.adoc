[[load-ldap]]
= Load LDAP

[abstract]
--
This section describes procedures that can be used to import data from LDAP.
--

With 'apoc.load.ldap' you can execute queries on any LDAP v3 enabled directory, the results are turned into a streams of entries.
The entries can then be used to update or create graph structures.

Note this utility requires to have the link:https://mvnrepository.com/artifact/org.apache.directory.api/apache-ldap-api/2.0.0[Apache LDAP API 2.0] library to be placed the plugin directory.

[separator=¦,opts=header,cols="1,1m,1m,5"]
|===
include::../../../build/generated-documentation/apoc.load.csv[lines=1;12]
|===

== Parameters

[options="header",cols="a,3m,a"]
|===
|Parameter | Property | Description
| url      | LDAP URL | See link:https://tools.ietf.org/html/rfc2255[RFC 2255] for the specifications
|{connectionMap} | username | This is the dn of the ldap server user who has read access on the ldap server
|  | password | This is the password used by the user
|  | pageSize | The number of results to return from the LDAP server at a time (default 100)
|===

Hint: Use an attribute parameter of * to return all attributes, + to return operational attributes, and no attributes will always return the distinguishedName

=== Load LDAP Example

.Retrieve group member information from the ldap server
[source,cypher]
----
call apoc.load.ldap("ldap://localhost:389/dc=neo4j,dc=test?uniqueMember,cn,objectClass?sub?(objectClass=groupOfUniqueNames)", {username: 'uid=admin,ou=system', password: 'secret'})
yield entry
return entry.dn,  entry.uniqueMember
----

[options="header",cols="3m,a"]
|===
| entry.dn                                 | entry.uniqueMember                                                                               |
| "cn=Machines,ou=Groups,dc=neo4j,dc=test" | ["cn=Agent Smith,ou=Machines,dc=neo4j,dc=test", "cn=The Architect,ou=Machines,dc=neo4j,dc=test"] |
| "cn=Humans,ou=Groups,dc=neo4j,dc=test"   | ["cn=Neo,ou=Users,dc=neo4j,dc=test", "cn=Morpheus,ou=Users,dc=neo4j,dc=test"]                    |
|===

.Retrieve group member information from the ldap server and create structure in Neo4j
[source,cypher]
----
call apoc.load.ldap("ldap://localhost:389/dc=neo4j,dc=test?uniqueMember,cn,objectClass?sub?(objectClass=groupOfUniqueNames)", {username: 'uid=admin,ou=system', password: 'secret'})
yield entry
merge (g:Group {dn : entry.dn})
on create set g.cn = entry.cn
foreach (member in entry.uniqueMember |
  merge (p:Person { dn : member })
  merge (p)-[:IS_MEMBER]->(g)
)
----

=== Credentials

To protect credentials, you can configure aliases in `conf/neo4j.conf`:

.neo4j.conf Syntax
----
apoc.ldap.localhost_auth.url=ldap://localhost:389/dc=neo4j,dc=test?uniqueMember,cn,objectClass?sub?(objectClass=groupOfUniqueNames)
apoc.ldap.localhost_auth.username=uid=admin,ou=system
apoc.ldap.localhost_auth.password=secret
apoc.ldap.localhost_auth.pageSize=250
----

Then

[source,cypher]
----
call apoc.load.ldap("ldap://localhost:389/dc=neo4j,dc=test?uniqueMember,cn,objectClass?sub?(objectClass=groupOfUniqueNames)", {credentials: {user: 'uid=admin,ou=system', password: 'secret'}})
yield entry
return entry.dn,  entry
----

becomes

[source,cypher]
----
call apoc.load.ldapfromconfig("localhost_auth")
yield entry
return entry.dn,  entry
----