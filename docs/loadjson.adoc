= Load JSON


Example for reverse geocoding and determining the route from one to another location.

[source,cypher]
----
WITH
        "21 rue Paul Bellamy 44000 NANTES FRANCE" AS fromAddr,
        "125 rue du docteur guichard 49000 ANGERS FRANCE" AS toAddr

call apoc.load.json("http://www.yournavigation.org/transport.php?url=http://nominatim.openstreetmap.org/search&format=json&q=" + replace(fromAddr, ' ', '%20')) YIELD value AS from

WITH from, toAddr  LIMIT 1

call apoc.load.json("http://www.yournavigation.org/transport.php?url=http://nominatim.openstreetmap.org/search&format=json&q=" + replace(toAddr, ' ', '%20')) YIELD value AS to

CALL apoc.load.json("https://router.project-osrm.org/viaroute?instructions=true&alt=true&z=17&loc=" + from.lat + "," + from.lon + "&loc=" + to.lat + "," + to.lon ) YIELD value AS doc

UNWIND doc.route_instructions as instruction

RETURN instruction
----

== Load Json with additional parameters

with `apoc.load.jsonParams` you can send additional headers or payload with your JSON GET request, e.g. for the Twitter API:


.Twitter Search via Cypher
[source,cypher]
----
WITH 'xxxx' as bearer
call apoc.load.jsonParams("https://api.twitter.com/1.1/search/tweets.json?q=oscon+OR+neo4j+OR+%23oscon+OR+%40neo4j&count=2&result_type=recent&lang=en",{Authorization:"Bearer "+bearer},null) yield value
UNWIND value.statuses as status
WITH status, status.user as u, status.entities as e
RETURN status.id, status.text, u.screen_name, [t IN e.hashtags | t.text] as tags, e.symbols, [m IN e.user_mentions | m.screen_name] as mentions, [u IN e.urls | u.expanded_url] as urls
----
